plugins {
    id 'project-android-application'
}

android {
    namespace 'nl.jjkester.crt.demo'

    defaultConfig {
        applicationId 'nl.jjkester.crt.demo'
        versionCode 1
        versionName project.version
    }

    kotlinOptions {
        freeCompilerArgs += [
                '-opt-in=androidx.compose.material3.ExperimentalMaterial3Api',
                '-opt-in=androidx.compose.animation.ExperimentalAnimationApi',
        ]
    }
}

dependencies {
    // App UI
    implementation 'androidx.activity:activity-compose:1.6.0'
    implementation 'androidx.compose.material3:material3:1.0.0-beta03'
    implementation 'androidx.lifecycle:lifecycle-viewmodel-compose:2.5.1'
    implementation libs.compose.ui.toolingPreview
    debugImplementation libs.compose.ui.tooling
    implementation 'androidx.navigation:navigation-compose:2.5.2'

    // Project dependencies
    implementation project(':crt-api')
    implementation project(':crt-common')
    implementation project(':crt-renderer-markdown')
    implementation project(':crt-builder-compose')

    implementation project(':utils:slf4j-android')
}

// Copy project README to resources to use in the demo app
task copyProjectReadmeToRawRes(type: Copy) {
    def sourceDir = rootProject.layout.projectDirectory
    def sourceFileName = 'README.md'
    def targetDir = layout.projectDirectory.dir('src/main/res/raw')
    def targetFileName = 'main_readme.md'

    from sourceDir.file(sourceFileName)
    into targetDir
    rename (sourceFileName, targetFileName)

    inputs.file(sourceDir.file(sourceFileName))
    outputs.file(targetDir.file(targetFileName))
}
tasks.findByName("preBuild").dependsOn(copyProjectReadmeToRawRes)
