pluginManagement {
    repositories {
        gradlePluginPortal()
        google()
        mavenCentral()
        maven {
            url 'https://maven.pkg.jetbrains.space/public/p/compose/dev'
        }
    }
}

dependencyResolutionManagement {
    repositoriesMode.set(RepositoriesMode.FAIL_ON_PROJECT_REPOS)
    repositories {
        google()
        mavenCentral()
    }

    versionCatalogs {
        libs {
            // Versions
            version('agp', '7.3.0')
            version('kotlin', '1.7.10')
            version('compose-compiler', '1.3.1')
            version('compose-ui', '1.2.1')
            version('compose-runtime', '1.2.1')
            version('compose-foundation', '1.2.1')

            // Plugins
            plugin('android-application', 'com.android.application').versionRef('agp')
            plugin('android-library', 'com.android.library').versionRef('agp')
            plugin('kotlin-jvm', 'org.jetbrains.kotlin.jvm').versionRef('kotlin')
            plugin('kotlin-android', 'org.jetbrains.kotlin.android').versionRef('kotlin')
            plugin('kotlin-dokka', 'org.jetbrains.dokka').versionRef('kotlin')
            plugin('kotlinx-binaryCompatibilityValidator', 'org.jetbrains.kotlinx.binary-compatibility-validator').version('0.11.1')

            // Libraries
            library('compose-ui-text', 'androidx.compose.ui', 'ui-text').versionRef('compose-ui')
            library('compose-ui', 'androidx.compose.ui', 'ui').versionRef('compose-ui')
            library('compose-runtime', 'androidx.compose.runtime', 'runtime').versionRef('compose-runtime')
            library('compose-foundation', 'androidx.compose.foundation', 'foundation').versionRef('compose-foundation')
            library('compose-ui-testManifest', 'androidx.compose.ui', 'ui-test-manifest').versionRef('compose-ui')
            library('compose-ui-tooling', 'androidx.compose.ui', 'ui-tooling').versionRef('compose-ui')
            library('compose-ui-toolingPreview', 'androidx.compose.ui', 'ui-tooling-preview').versionRef('compose-ui')
            library('slf4j-api', 'org.slf4j', 'slf4j-api').version('2.0.3')
        }
    }
}

include ':crt-api-builder'
include ':crt-api-common'
include ':crt-api-renderer'
include ':crt-builder-compose'
include ':crt-common'
include ':crt-demo'
include ':crt-renderer-markdown'
include ':utils:slf4j-android'
